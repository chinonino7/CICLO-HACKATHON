// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

/// @title COPm de PRUEBA para Celo Sepolia
/// @notice El cCOP real solo existe en mainnet. Este ERC20 lo simula en testnet:
///         cualquiera puede emitirse saldo con faucet(). NO desplegar en mainnet.
contract TestCOPm {
    string public constant name = "Peso Colombiano (prueba)";
    string public constant symbol = "COPm";
    uint8 public constant decimals = 18;

    uint256 public totalSupply;
    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance;

    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /// @notice Emite 1.000.000 COPm de prueba a quien llame.
    function faucet() external {
        _mint(msg.sender, 1_000_000e18);
    }

    function mint(address to, uint256 value) external {
        _mint(to, value);
    }

    function transfer(address to, uint256 value) external returns (bool) {
        return _transfer(msg.sender, to, value);
    }

    function approve(address spender, uint256 value) external returns (bool) {
        allowance[msg.sender][spender] = value;
        emit Approval(msg.sender, spender, value);
        return true;
    }

    function transferFrom(address from, address to, uint256 value) external returns (bool) {
        uint256 allowed = allowance[from][msg.sender];
        require(allowed >= value, "allowance");
        if (allowed != type(uint256).max) allowance[from][msg.sender] = allowed - value;
        return _transfer(from, to, value);
    }

    function _transfer(address from, address to, uint256 value) internal returns (bool) {
        require(to != address(0), "to=0");
        require(balanceOf[from] >= value, "balance");
        balanceOf[from] -= value;
        balanceOf[to] += value;
        emit Transfer(from, to, value);
        return true;
    }

    function _mint(address to, uint256 value) internal {
        totalSupply += value;
        balanceOf[to] += value;
        emit Transfer(address(0), to, value);
    }
}
